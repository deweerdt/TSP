
IF (DOXYGEN_FOUND)
  
  SET(DOXYGEN_INPUT ${CMAKE_BINARY_DIR}/tsp.doxy)
  SET(STATIC_PAGES  ${CMAKE_CURRENT_SOURCE_DIR}/footer/footer.html ${CMAKE_CURRENT_SOURCE_DIR}/header/header.html)
  SET(APIDOC_DIR ${CMAKE_BINARY_DIR}/doc/api CACHE PATH "API documentation directory")
  SET(DOXYGEN_OUTPUT ${APIDOC_DIR}/html/index.html)

IF (WIN32)
  SET(DOXYGEN_GENERATE_MAN NO)
  FIND_PROGRAM(HHC_PROGRAM 
    NAMES hhc.exe 
    PATHS "c:/Program Files/HTML Help Workshop"
    DOC "HTML Help Compiler program")
ELSE (WIN32)
  SET(DOXYGEN_GENERATE_MAN YES)
  SET(HHC_PROGRAM)
ENDIF (WIN32)

TSP_CONFIGURE_FILE_AUTOTOOLS_COMPAT(
  ${CMAKE_CURRENT_SOURCE_DIR}/tsp.in 
  ${CMAKE_BINARY_DIR}/tsp.doxy)

FILE(MAKE_DIRECTORY ${APIDOC_DIR}/html)

CONFIGURE_FILE(${CMAKE_CURRENT_SOURCE_DIR}/tsp.css ${APIDOC_DIR}/html/tsp.css COPYONLY)
FILE(GLOB header_files RELATIVE ${CMAKE_CURRENT_SOURCE_DIR} ${CMAKE_CURRENT_SOURCE_DIR}/header/*.htm*)
FILE(GLOB footer_files RELATIVE ${CMAKE_CURRENT_SOURCE_DIR} ${CMAKE_CURRENT_SOURCE_DIR}/footer/*.htm*)
FILE(GLOB images_files RELATIVE ${CMAKE_CURRENT_SOURCE_DIR} ${CMAKE_CURRENT_SOURCE_DIR}/images/*.png)

# Copy necessary doc files
FOREACH (file ${header_files} ${footer_files} ${images_files})
  CONFIGURE_FILE(${CMAKE_CURRENT_SOURCE_DIR}/${file} ${APIDOC_DIR}/html/${file} COPYONLY)
ENDFOREACH (file)

ADD_CUSTOM_COMMAND(
  OUTPUT ${DOXYGEN_OUTPUT}
  COMMAND ${CMAKE_COMMAND} -E echo_append "Building API Documentation..."
  COMMAND ${DOXYGEN_EXECUTABLE} ${DOXYGEN_INPUT}
  COMMAND ${CMAKE_COMMAND} -E echo "Done."
  WORKING_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}
  DEPENDS ${DOXYGEN_INPUT}
  )

ADD_CUSTOM_TARGET(apidoc ALL DEPENDS ${DOXYGEN_OUTPUT})

ADD_CUSTOM_TARGET(apidoc_forced
  COMMAND ${CMAKE_COMMAND} -E echo_append "Building API Documentation..."
  COMMAND ${DOXYGEN_EXECUTABLE} ${DOXYGEN_INPUT}
  COMMAND ${CMAKE_COMMAND} -E echo "Done."
  WORKING_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR})

  
FILE(GLOB_RECURSE doc_files RELATIVE ${APIDOC_DIR} ${APIDOC_DIR}/*)
FOREACH(file ${doc_files})
  GET_FILENAME_COMPONENT(file_prefix ${file} PATH)
  INSTALL(FILES ${APIDOC_DIR}/${file}
    DESTINATION doc/${file_prefix})
ENDFOREACH(file)

ENDIF (DOXYGEN_FOUND)
